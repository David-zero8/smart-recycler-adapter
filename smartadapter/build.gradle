apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'maven-publish'

ext {
    LIB_ARTIFACT_NAME = PROJ_NAME
    LIB_ARTIFACT_ID = PROJ_ARTIFACT_ID
    LIB_VERSION_CODE = PROJ_VERSION_CODE
    LIB_VERSION_NAME = PROJ_VERSION_NAME
    LIB_DESCRIPTION = 'Never code any boilerplate RecyclerAdapter again! This library will make it easy and painless to map your data item with a target ViewHolder.'
}

android {
    namespace "io.github.zero8.smartrecycleradapter"
    compileSdkVersion TARGET_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode PROJ_VERSION_CODE
        versionName PROJ_VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // setProperty("archivesBaseName", "$LIB_ARTIFACT_ID-$LIB_VERSION_NAME") TODO Not working with com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4
        setProperty("archivesBaseName", "$LIB_ARTIFACT_ID")
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/kotlin']
            res.srcDirs = ['src/main/res']
        }
        release {
            java.srcDirs = ['src/main/kotlin']
            res.srcDirs = ['src/main/res']
        }
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding = true
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs = ['-Xjvm-default=all']
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Test
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.robolectric:robolectric:4.3'

    // Androidx
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

if (project.rootProject.file('local.properties').exists()) {
    apply from: '../maven-local-publish.gradle'
    apply from: '../maven-release-config.gradle'
    apply from: '../maven-release-script.gradle'
}