apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    namespace "io.github.zero8.smartrecycleradapter.sample"
    compileSdkVersion TARGET_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {

        applicationId "io.github.zero8.smartrecycleradapter.sample"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode PROJ_VERSION_CODE
        versionName PROJ_VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        setProperty("archivesBaseName", "$PROJ_ARTIFACT_ID-$PROJ_VERSION_NAME")
    }

    signingConfigs {
        release {
            try {
                storeFile file(properties['storeFile'])
                storePassword properties['storePassword']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            } catch (e) {
                // Silent fail
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "app_name", "SmartAdapter"
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            resValue "string", "app_name", "Debug"
        }
        beta {
            initWith debug
            applicationIdSuffix ".beta"
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['debug']
            resValue "string", "app_name", "Beta"
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/kotlin']
            res.srcDirs = ['src/main/res']
        }
        release {
            java.srcDirs = ['src/main/kotlin']
            res.srcDirs = ['src/main/res']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs = ['-Xjvm-default=all']
    }

    buildFeatures {
        dataBinding = true
        buildConfig = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    testImplementation 'junit:junit:4.12'

    // Project
    //implementation 'io.github.manneohlund:smart-recycler-adapter:5.0.0-rc01'
    //implementation 'io.github.manneohlund:smart-recycler-adapter-viewevent:1.0.0-beta03'
    //implementation 'io.github.manneohlund:smart-recycler-adapter-diffutil:1.0.0-beta01'
    //implementation 'io.github.manneohlund:smart-recycler-adapter-nestedadapter:1.0.0-beta01'
    //implementation 'io.github.manneohlund:smart-recycler-adapter-stickyheader:1.0.0-alpha02'
    //implementation 'io.github.manneohlund:smart-recycler-adapter-filter:1.0.0-alpha01'

    implementation project(':smartadapter')
    implementation project(path: ':extensions:viewevent')
    implementation project(path: ':extensions:nestedadapter')
    implementation project(path: ':extensions:stickyheader')
    implementation project(path: ':extensions:filter')

    // Androidx
    implementation "androidx.annotation:annotation:$annotation_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.activity:activity-ktx:$activity_ktx_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "com.google.android.material:material:$material_version"

    // Kotlin
//    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Other
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.google.code.gson:gson:2.8.6'
}
